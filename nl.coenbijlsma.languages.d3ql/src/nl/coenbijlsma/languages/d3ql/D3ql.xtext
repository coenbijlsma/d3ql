grammar nl.coenbijlsma.languages.d3ql.D3ql with org.eclipse.xtext.common.Terminals

generate d3ql "http://www.coenbijlsma.nl/languages/d3ql/D3ql"

Query:
	fromStatement=FromStatement selectStatement=SelectStatement
;

FromStatement:
	'from' aggregates+=AggregateRoot (',' aggregates+=AggregateRoot)*
;

Named:
	AggregateRoot | Alias
;

AggregateRoot:
	name=ID ('as' alias=Alias)?
;

Alias:
	name=ID
;

SelectStatement:
	'select' expressions+=SelectExpression (',' expressions+=SelectExpression)*
;

SelectExpression:
	expression=(PathExpression | FunctionCall | Literal) ('as' alias=Alias)?
;

PathExpression:
	head=[Named] ('.' (tail+=PathElement '.')* tail+=PathElement)?
;

PathElement:
	name=ID
;

FunctionCall:
	function=ID '(' arguments+=FunctionArgument (',' arguments+=FunctionArgument)*  ')'
;

FunctionArgument:
	value=(PathExpression | Literal)
;

Literal:
	IntegerLiteral | StringLiteral | BooleanLiteral
;

IntegerLiteral:
	value=INT
;

StringLiteral:
	value=STRING
;

BooleanLiteral:
	value=('true' | 'false')
;